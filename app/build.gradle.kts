import java.util.Properties
import java.util.zip.ZipFile
import org.gradle.api.GradleException

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.hilt.android)
    alias(libs.plugins.ksp)
    alias(libs.plugins.kotlin.serialization)
}

val enableNativeBuild = (findProperty("enableNativeBuild") as? String)?.toBoolean() ?: false
val isTermuxHost = System.getenv("TERMUX_VERSION") != null ||
    (System.getenv("PREFIX")?.contains("com.termux") == true)
// 支持工作流通过 -PversionName/-PversionCode 覆盖版本
val configuredVersionName = findProperty("versionName")
    ?.toString()
    ?.trim()
    ?.takeIf { it.isNotEmpty() }
    ?: "1.0.5.38"
val configuredVersionCode = findProperty("versionCode")
    ?.toString()
    ?.trim()
    ?.toIntOrNull()
    ?.takeIf { it > 0 }
    ?: 119
val defaultReleaseAbis = listOf("arm64-v8a", "armeabi-v7a", "x86_64")
val rawAbiFilters = (findProperty("abiFilters") as? String)
    ?.split(',')
    ?.map { it.trim() }
    ?.filter { it.isNotEmpty() }
    ?.distinct()
    .orEmpty()
val unsupportedAbiFilters = rawAbiFilters.filterNot { it in defaultReleaseAbis }
if (unsupportedAbiFilters.isNotEmpty()) {
    throw GradleException(
        "不支持的 abiFilters: ${unsupportedAbiFilters.joinToString(",")}，仅支持: ${defaultReleaseAbis.joinToString(",")}"
    )
}
val configuredAbiFilters = if (rawAbiFilters.isEmpty()) defaultReleaseAbis else rawAbiFilters
val requestedTaskNames = gradle.startParameter.taskNames.map { it.lowercase() }
val isBundleTaskRequested = requestedTaskNames.any { it.contains("bundle") }

fun parseBooleanProperty(name: String): Boolean? {
    val rawValue = (findProperty(name) as? String)?.trim().orEmpty()
    if (rawValue.isBlank()) return null
    return when (rawValue.lowercase()) {
        "true" -> true
        "false" -> false
        else -> throw GradleException("$name 仅支持 true/false，当前值: $rawValue")
    }
}

val configuredEnableProguard = parseBooleanProperty("enableProguard")
val configuredShrinkResources = parseBooleanProperty("shrinkResources")
val defaultShrinkResources = true

val localProps = Properties().apply {
    val propsFile = rootProject.file("local.properties")
    if (propsFile.exists()) {
        propsFile.inputStream().use { load(it) }
    }
}

val legacyProjectDir = file("/data/user/0/com.termux/files/home/danmu-api-android-main")
val legacyLocalProps = Properties().apply {
    val propsFile = legacyProjectDir.resolve("local.properties")
    if (propsFile.exists()) {
        propsFile.inputStream().use { load(it) }
    }
}

fun resolveSigningValue(envKeys: List<String>, propKeys: List<String>): String? {
    envKeys.forEach { key ->
        val value = System.getenv(key)?.trim().orEmpty()
        if (value.isNotBlank()) return value
    }
    propKeys.forEach { key ->
        val gradleValue = (findProperty(key) as? String)?.trim().orEmpty()
        if (gradleValue.isNotBlank()) return gradleValue
        val localValue = localProps.getProperty(key)?.trim().orEmpty()
        if (localValue.isNotBlank()) return localValue
        val legacyValue = legacyLocalProps.getProperty(key)?.trim().orEmpty()
        if (legacyValue.isNotBlank()) return legacyValue
    }
    return null
}

fun isUsableKeystore(file: java.io.File): Boolean {
    return file.exists() && file.isFile && file.length() > 128L
}

val defaultLegacyKeystore = legacyProjectDir.resolve("danmuapi-ci.jks")
val defaultPrimaryKeystore = rootProject.file("danmuapi-ci.jks")
val fallbackKeystore = rootProject.file("keystore.jks")
val configuredStorePath = resolveSigningValue(
    envKeys = listOf("ANDROID_KEYSTORE_PATH"),
    propKeys = listOf("keystore.path")
)
val resolvedStoreFile = sequenceOf(
    configuredStorePath?.let { file(it) },
    defaultLegacyKeystore,
    defaultPrimaryKeystore,
    fallbackKeystore
).filterNotNull().firstOrNull { isUsableKeystore(it) }
val resolvedStorePassword = resolveSigningValue(
    envKeys = listOf("ANDROID_KEYSTORE_PASSWORD", "KS_PASS"),
    propKeys = listOf("keystore.password", "KS_PASS")
)
val resolvedKeyAlias = resolveSigningValue(
    envKeys = listOf("ANDROID_KEY_ALIAS", "KEY_ALIAS"),
    propKeys = listOf("key.alias", "KEY_ALIAS")
) ?: "danmuapi"
val resolvedKeyPassword = resolveSigningValue(
    envKeys = listOf("ANDROID_KEY_PASSWORD", "KEY_PASS"),
    propKeys = listOf("key.password", "KEY_PASS")
) ?: resolvedStorePassword
val useProjectSigning = resolvedStoreFile != null &&
    !resolvedStorePassword.isNullOrBlank() &&
    resolvedKeyAlias.isNotBlank() &&
    !resolvedKeyPassword.isNullOrBlank()

android {
    namespace = "com.example.danmuapiapp"
    compileSdk = 36

    defaultConfig {
        applicationId = "com.example.danmuapiapp"
        minSdk = 23
        targetSdk = 36
        versionCode = configuredVersionCode
        versionName = configuredVersionName
    }

    signingConfigs {
        create("projectSign") {
            if (useProjectSigning) {
                storeFile = resolvedStoreFile
                storePassword = resolvedStorePassword!!
                keyAlias = resolvedKeyAlias
                keyPassword = resolvedKeyPassword!!
            }
        }
    }

    buildTypes {
        debug {
            if (useProjectSigning) {
                signingConfig = signingConfigs.getByName("projectSign")
            }
        }
        release {
            val minifyEnabled = configuredEnableProguard ?: true
            isMinifyEnabled = minifyEnabled
            // 资源压缩依赖 R8，禁用混淆时自动关闭资源压缩。
            isShrinkResources = (configuredShrinkResources ?: defaultShrinkResources) && minifyEnabled
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
            if (useProjectSigning) {
                signingConfig = signingConfigs.getByName("projectSign")
            }
        }
    }

    splits {
        abi {
            // AAB 不走本地 ABI split，避免 shrinkResources 与 split 的已知冲突。
            isEnable = !isBundleTaskRequested
            reset()
            if (isEnable) {
                include(*configuredAbiFilters.toTypedArray())
            }
            isUniversalApk = false
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
        isCoreLibraryDesugaringEnabled = true
    }

    buildFeatures {
        compose = true
        buildConfig = true
    }

    packaging {
        jniLibs {
            // 维持历史打包方式，减小 APK 体积
            useLegacyPackaging = true
            // Termux 下跳过 AGP 的 strip（其依赖的宿主工具不可用），改由自定义任务预裁剪
            if (isTermuxHost) {
                keepDebugSymbols += "**/*.so"
            }
        }
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }

    // CMake 原生构建可按需开启，默认关闭以兼容 Termux 本机构建
    if (enableNativeBuild) {
        externalNativeBuild {
            cmake {
                path = file("CMakeLists.txt")
                version = "3.22.1"
            }
        }
    }

    sourceSets {
        getByName("main") {
            if (isTermuxHost) {
                val termuxJniDirs = mutableListOf("${layout.buildDirectory.get().asFile}/termux-jni-libs/libnode/bin")
                if (!enableNativeBuild) {
                    termuxJniDirs += "${layout.buildDirectory.get().asFile}/termux-jni-libs/jni-current"
                }
                jniLibs.directories.clear()
                jniLibs.directories.addAll(termuxJniDirs)
            } else {
                val jniDirs = mutableListOf("libnode/bin")
                if (!enableNativeBuild) {
                    // 使用当前包名重新编译的 JNI 桥接库，避免旧符号导致 UnsatisfiedLinkError
                    jniDirs += "jni-current"
                }
                jniLibs.directories.clear()
                jniLibs.directories.addAll(jniDirs)
            }
        }
    }
}

kotlin {
    compilerOptions {
        freeCompilerArgs.add("-Xannotation-default-target=param-property")
    }
}

dependencies {
    // Compose BOM
    val composeBom = platform(libs.compose.bom)
    implementation(composeBom)
    implementation(libs.compose.ui)
    implementation(libs.compose.ui.graphics)
    implementation(libs.compose.material3)
    implementation(libs.compose.material.icons)
    implementation(libs.compose.animation)
    implementation(libs.compose.foundation)
    debugImplementation(libs.compose.ui.tooling)
    implementation(libs.compose.ui.tooling.preview)

    // AndroidX
    implementation(libs.core.ktx)
    implementation(libs.activity.compose)
    implementation(libs.splashscreen)
    implementation(libs.datastore.prefs)
    implementation(libs.documentfile)

    // Navigation
    implementation(libs.navigation.compose)

    // Lifecycle
    implementation(libs.lifecycle.runtime.compose)
    implementation(libs.lifecycle.viewmodel.compose)

    // Hilt DI
    implementation(libs.hilt.android)
    ksp(libs.hilt.compiler)
    implementation(libs.hilt.navigation.compose)

    // Coroutines
    implementation(libs.coroutines.android)

    // Serialization
    implementation(libs.serialization.json)

    // Network
    implementation(libs.okhttp)

    // Image loading
    implementation(libs.coil.compose)

    // 提供 XML 主题 Theme.Material3.DayNight.NoActionBar
    implementation(libs.material)

    // QR Code
    implementation(libs.zxing.core)

    // Java 8+ API（如 java.time）向低版本兼容
    coreLibraryDesugaring(libs.desugar.jdk.libs)

    testImplementation("junit:junit:4.13.2")

}

// 清理项目内控制字符文件名，避免异常垃圾文件混入仓库
tasks.register("cleanupGarbageFiles") {
    doLast {
        val appRoot = projectDir
        val targets = appRoot.walkBottomUp().filter { file ->
            file.name.any { ch -> ch.code < 32 || ch.code == 127 }
        }.toList()
        targets.forEach { file ->
            if (file.isDirectory) file.deleteRecursively() else file.delete()
            println("已删除垃圾文件：${file.relativeTo(appRoot).invariantSeparatorsPath}")
        }
    }
}

fun org.gradle.api.file.CopySpec.includeNodeModuleDirs(packages: Iterable<String>) {
    packages.forEach { pkg ->
        include("$pkg/**")
    }
}

fun normalizeNodeDependencyVersion(raw: String): String {
    val value = raw.trim()
    return value.removePrefix("^").removePrefix("~").trim()
}

fun readBundledNodeDependencyNames(): List<String> {
    val packageJsonFile = file("src/main/assets/nodejs-project/package.json")
    if (!packageJsonFile.exists()) {
        throw GradleException("缺少运行时依赖声明文件：${packageJsonFile.absolutePath}")
    }
    val pkg = groovy.json.JsonSlurper().parse(packageJsonFile) as? Map<*, *>
        ?: throw GradleException("无法解析 package.json：${packageJsonFile.absolutePath}")
    val dependencies = (pkg["dependencies"] as? Map<*, *>)?.keys
        ?.mapNotNull { it?.toString()?.trim()?.takeIf(String::isNotBlank) }
        .orEmpty()
    if (dependencies.isEmpty()) {
        throw GradleException("package.json 未声明任何运行时依赖：${packageJsonFile.absolutePath}")
    }
    return dependencies
}

fun pruneNodeModuleRuntimeNoise(rootDir: java.io.File) {
    if (!rootDir.exists() || !rootDir.isDirectory) return

    val redundantDocName = Regex("""(?i)^(readme|changelog|history)(\..*)?$""")
    val redundantPakoDistFiles = setOf(
        "pako/dist/pako.es5.js",
        "pako/dist/pako.es5.min.js",
        "pako/dist/pako.js",
        "pako/dist/pako.min.js",
        "pako/dist/pako_deflate.es5.js",
        "pako/dist/pako_deflate.es5.min.js",
        "pako/dist/pako_deflate.js",
        "pako/dist/pako_deflate.min.js",
        "pako/dist/pako_inflate.es5.js",
        "pako/dist/pako_inflate.es5.min.js",
        "pako/dist/pako_inflate.js",
        "pako/dist/pako_inflate.min.js"
    )

    rootDir.walkTopDown()
        .filter { it.isFile }
        .forEach { file ->
            val relativePath = file.relativeTo(rootDir).invariantSeparatorsPath
            val shouldDelete =
                relativePath.endsWith(".map") ||
                    relativePath.endsWith(".d.ts") ||
                    redundantDocName.matches(file.name) ||
                    relativePath in redundantPakoDistFiles

            if (shouldDelete) {
                file.delete()
            }
        }

    rootDir.walkBottomUp()
        .filter { it.isDirectory && it != rootDir && it.list()?.isEmpty() == true }
        .forEach { it.delete() }
}

val baseNodeModulesPackages = buildList {
    addAll(readBundledNodeDependencyNames())
    addAll(
        listOf(
            "agent-base",
            "debug",
            "ms",
            "data-uri-to-buffer",
            "fetch-blob",
            "formdata-polyfill",
            "node-domexception",
            "web-streams-polyfill"
        )
    )
}.distinct()
val optionalRedisNodeModulesPackages = listOf(
    "redis",
    "@redis",
    "cluster-key-slot"
)

// 生成基础运行时依赖与可选 redis 依赖包，避免每次更新都预解压完整依赖树。
tasks.register("prepareNodeModules") {
    val zipFile = rootProject.file("node_modules.zip")
    val baseTargetDir = file("src/main/assets/nodejs-project/node_modules")
    val optionalRootDir = file("src/main/assets/nodejs-optional")
    val optionalRedisTargetDir = file("src/main/assets/nodejs-optional/redis/node_modules")
    val tempRootDir = layout.buildDirectory.dir("prepared-node-modules").get().asFile
    if (zipFile.exists()) {
        inputs.file(zipFile)
    }
    outputs.dirs(baseTargetDir, optionalRootDir)
    dependsOn("cleanupGarbageFiles")
    doLast {
        val sourceRoot = File(tempRootDir, "source")
        val sourceNodeModules = File(sourceRoot, "node_modules")
        val splitRedisReady = File(optionalRedisTargetDir, "redis/package.json").exists()
        val baseStillContainsRedis =
            File(baseTargetDir, "redis/package.json").exists() || File(baseTargetDir, "@redis").exists()

        delete(tempRootDir)
        sourceRoot.mkdirs()

        when {
            zipFile.exists() -> {
                copy {
                    from(zipTree(zipFile)) {
                        include("node_modules/**")
                        includeEmptyDirs = false
                        eachFile {
                            val normalized = path.removePrefix("node_modules/")
                            if (normalized == path || normalized.isBlank()) {
                                exclude()
                            } else {
                                if (normalized.any { ch -> ch.code < 32 || ch.code == 127 }) {
                                    throw GradleException("node_modules.zip 含非法文件名：$path")
                                }
                                path = normalized
                            }
                        }
                    }
                    into(sourceRoot)
                }
            }
            splitRedisReady -> {
                copy {
                    from(baseTargetDir)
                    into(sourceNodeModules)
                }
                copy {
                    from(optionalRedisTargetDir)
                    into(sourceNodeModules)
                }
            }
            baseTargetDir.exists() && baseStillContainsRedis -> {
                copy {
                    from(baseTargetDir)
                    into(sourceNodeModules)
                }
            }
            baseTargetDir.exists() -> {
                copy {
                    from(baseTargetDir)
                    into(sourceNodeModules)
                }
            }
            else -> return@doLast
        }

        val workspaceNodeModules = rootProject.file("../danmu_api/node_modules")
        if (workspaceNodeModules.exists()) {
            copy {
                from(workspaceNodeModules) {
                    includeNodeModuleDirs(baseNodeModulesPackages + optionalRedisNodeModulesPackages)
                    includeEmptyDirs = false
                }
                into(sourceNodeModules)
            }
        }

        pruneNodeModuleRuntimeNoise(sourceNodeModules)

        delete(baseTargetDir)
        delete(optionalRootDir)

        if (!sourceNodeModules.exists()) return@doLast

        copy {
            from(sourceNodeModules) {
                includeNodeModuleDirs(baseNodeModulesPackages)
                includeEmptyDirs = false
            }
            into(baseTargetDir)
        }

        val redisSourceReady =
            File(sourceNodeModules, "redis/package.json").exists() || File(sourceNodeModules, "@redis").exists()
        if (redisSourceReady) {
            copy {
                from(sourceNodeModules) {
                    includeNodeModuleDirs(optionalRedisNodeModulesPackages)
                    includeEmptyDirs = false
                }
                into(optionalRedisTargetDir)
            }
        }
    }
}

tasks.register("syncBundledNodeModulesFromWorkspace") {
    val workspaceNodeModules = rootProject.file("../danmu_api/node_modules")
    val baseTargetDir = file("src/main/assets/nodejs-project/node_modules")
    val optionalRedisTargetDir = file("src/main/assets/nodejs-optional/redis/node_modules")
    inputs.dir(workspaceNodeModules)
    outputs.dirs(baseTargetDir, optionalRedisTargetDir)
    doLast {
        if (!workspaceNodeModules.exists()) {
            throw GradleException("未找到工作区依赖目录：${workspaceNodeModules.absolutePath}")
        }
        baseTargetDir.mkdirs()
        optionalRedisTargetDir.mkdirs()
        copy {
            from(workspaceNodeModules) {
                includeNodeModuleDirs(baseNodeModulesPackages)
                includeEmptyDirs = false
            }
            into(baseTargetDir)
        }
        copy {
            from(workspaceNodeModules) {
                includeNodeModuleDirs(optionalRedisNodeModulesPackages)
                includeEmptyDirs = false
            }
            into(optionalRedisTargetDir)
        }
        pruneNodeModuleRuntimeNoise(baseTargetDir)
        pruneNodeModuleRuntimeNoise(optionalRedisTargetDir)
    }
}

tasks.register("verifyBundledNodeModules") {
    val packageJsonFile = file("src/main/assets/nodejs-project/package.json")
    val nodeModulesDir = file("src/main/assets/nodejs-project/node_modules")
    dependsOn("prepareNodeModules")
    dependsOn("syncBundledNodeModulesFromWorkspace")
    inputs.file(packageJsonFile)
    inputs.dir(nodeModulesDir)
    doLast {
        if (!packageJsonFile.exists()) {
            throw GradleException("缺少运行时依赖声明文件：${packageJsonFile.absolutePath}")
        }

        val pkg = groovy.json.JsonSlurper().parse(packageJsonFile) as? Map<*, *>
            ?: throw GradleException("无法解析 package.json：${packageJsonFile.absolutePath}")
        val dependencies = (pkg["dependencies"] as? Map<*, *>)?.mapNotNull { (key, value) ->
            val name = key?.toString()?.trim().orEmpty()
            val version = value?.toString()?.trim().orEmpty()
            if (name.isBlank() || version.isBlank()) null else name to normalizeNodeDependencyVersion(version)
        }.orEmpty()

        val missing = mutableListOf<String>()
        val mismatched = mutableListOf<String>()
        dependencies.forEach { (name, expectedVersion) ->
            val depPkg = file("src/main/assets/nodejs-project/node_modules/$name/package.json")
            if (!depPkg.exists()) {
                missing += "$name@$expectedVersion"
                return@forEach
            }
            val depJson = groovy.json.JsonSlurper().parse(depPkg) as? Map<*, *>
            val actualVersion = depJson?.get("version")?.toString()?.trim().orEmpty()
            if (actualVersion.isBlank()) {
                missing += "$name@$expectedVersion"
            } else if (actualVersion != expectedVersion) {
                mismatched += "$name 期望 $expectedVersion，实际 $actualVersion"
            }
        }

        if (missing.isNotEmpty() || mismatched.isNotEmpty()) {
            val details = buildList {
                if (missing.isNotEmpty()) add("缺少依赖：${missing.joinToString(", ")}")
                if (mismatched.isNotEmpty()) add("版本不匹配：${mismatched.joinToString("；")}")
            }.joinToString("；")
            throw GradleException("运行时 assets 依赖校验失败：$details")
        }
    }
}

tasks.register<Exec>("checkNodeRuntimeScripts") {
    commandLine("node", "--check", "src/main/assets/nodejs-project/android-server.mjs")
}

tasks.register<Exec>("testNodeRuntimeParsing") {
    workingDir = rootProject.projectDir
    commandLine("node", "node-tests/parse-dotenv-regression.mjs")
}

tasks.register("verifyPackagedNodeModulesDebug") {
    val apkFile = layout.buildDirectory.file("outputs/apk/debug/app-arm64-v8a-debug.apk")
    dependsOn("assembleDebug")
    inputs.file(apkFile)
    doLast {
        val file = apkFile.get().asFile
        if (!file.exists()) throw GradleException("未找到 debug APK：${file.absolutePath}")
        val requiredEntries = listOf(
            "assets/nodejs-project/node_modules/node-fetch/package.json",
            "assets/nodejs-project/node_modules/pako/package.json"
        )
        ZipFile(file).use { zip ->
            val missing = requiredEntries.filter { zip.getEntry(it) == null }
            if (missing.isNotEmpty()) {
                throw GradleException("Debug APK 缺少关键依赖文件：${missing.joinToString(", ")}")
            }
        }
    }
}

tasks.register("verifyPackagedNodeModulesRelease") {
    val apkFile = layout.buildDirectory.file("outputs/apk/release/app-arm64-v8a-release.apk")
    dependsOn("assembleRelease")
    inputs.file(apkFile)
    doLast {
        val file = apkFile.get().asFile
        if (!file.exists()) throw GradleException("未找到 release APK：${file.absolutePath}")
        val requiredEntries = listOf(
            "assets/nodejs-project/node_modules/node-fetch/package.json",
            "assets/nodejs-project/node_modules/pako/package.json"
        )
        ZipFile(file).use { zip ->
            val missing = requiredEntries.filter { zip.getEntry(it) == null }
            if (missing.isNotEmpty()) {
                throw GradleException("Release APK 缺少关键依赖文件：${missing.joinToString(", ")}")
            }
        }
    }
}

val prepareNodeModulesTask = tasks.named("prepareNodeModules")
val syncBundledNodeModulesTask = tasks.named("syncBundledNodeModulesFromWorkspace")
val verifyBundledNodeModulesTask = tasks.named("verifyBundledNodeModules")
tasks.named("preBuild").configure {
    dependsOn(prepareNodeModulesTask)
    dependsOn(syncBundledNodeModulesTask)
    dependsOn(verifyBundledNodeModulesTask)
}

tasks.matching {
    (it.name.startsWith("merge") && it.name.endsWith("Assets")) ||
        it.name.startsWith("lintAnalyze") ||
        (it.name.startsWith("generate") && it.name.endsWith("LintReportModel")) ||
        (it.name.startsWith("generate") && it.name.endsWith("LintVitalReportModel")) ||
        it.name.contains("lintVital", ignoreCase = true)
}.configureEach {
    dependsOn(prepareNodeModulesTask)
    dependsOn(syncBundledNodeModulesTask)
    dependsOn(verifyBundledNodeModulesTask)
}

// Termux 下预裁剪 JNI so，避免 AGP strip 工具链与宿主架构不兼容导致体积异常
tasks.register("prepareTermuxJniLibs") {
    if (isTermuxHost) {
        val outRoot = layout.buildDirectory.dir("termux-jni-libs").get().asFile
        outputs.dir(outRoot)
        doLast {
            delete(outRoot)

            val outLibnode = File(outRoot, "libnode/bin")
            copy {
                from(file("libnode/bin"))
                into(outLibnode)
            }
            if (!enableNativeBuild) {
                val outJniCurrent = File(outRoot, "jni-current")
                copy {
                    from(file("jni-current"))
                    into(outJniCurrent)
                }
            }

            val stripPathCandidates = listOf(
                System.getenv("LLVM_STRIP")?.trim().orEmpty(),
                (System.getenv("PREFIX")?.trim().orEmpty() + "/bin/llvm-strip").trim(),
                "/data/data/com.termux/files/usr/bin/llvm-strip",
                "llvm-strip"
            ).filter { it.isNotBlank() }

            val stripTool = stripPathCandidates.firstOrNull { candidate ->
                if (candidate.contains('/')) File(candidate).exists() else true
            } ?: "llvm-strip"

            val soFiles = outRoot.walkTopDown()
                .filter { it.isFile && it.extension == "so" }
                .toList()
            soFiles.forEach { so ->
                runCatching {
                    val process = ProcessBuilder(
                        stripTool,
                        "--strip-unneeded",
                        so.absolutePath
                    ).redirectErrorStream(true).start()
                    process.inputStream.bufferedReader(Charsets.UTF_8).use { it.readText() }
                    val exit = process.waitFor()
                    if (exit != 0) {
                        throw GradleException("strip exit=$exit")
                    }
                }.onFailure {
                    println("警告：预裁剪失败 ${so.absolutePath} -> ${it.message}")
                }
            }
        }
    }
}

tasks.matching {
    (it.name.startsWith("merge") && (it.name.endsWith("JniLibFolders") || it.name.endsWith("NativeLibs"))) ||
        (it.name.startsWith("strip") && it.name.endsWith("DebugSymbols"))
}.configureEach {
    if (isTermuxHost) {
        dependsOn("prepareTermuxJniLibs")
    }
}
